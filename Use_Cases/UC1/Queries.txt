#We have one mutation of interest (rs4784227-> chr16:52565276) and we want to study its possible implications in the regulation of gene expression. 
#1. Is the mutation located in any enhancer sequence of the network? 
# What databases support the sequence? Is the enhancer related to any disease? 
# Does the enhancer have any target genes according to the sources? 
# Which proteins encode the gene?

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX nuccore: <https://www.ncbi.nlm.nih.gov/nuccore/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT DISTINCT ?name_database ?enh_label ?start ?end ?disease ?tgene_label ?prot_label
WHERE {
        GRAPH <http://rdf.biogateway.eu/graph/crm> {
                ?enhID rdfs:subClassOf obo:SO_0000727 ;
      			obo:GENO_0000894 ?start ;
                        obo:GENO_0000895 ?end ;
                        obo:BFO_0000050 nuccore:NC_000016.10 ;
                        skos:prefLabel ?enh_label .

		?enh_inst rdf:type ?enhID ;
                        sio:SIO_000253 ?database .
    					
    		?database skos:prefLabel ?name_database
                FILTER (?start <= 52565276 && ?end >= 52565276)
  	}

	OPTIONAL {
		GRAPH <http://rdf.biogateway.eu/graph/crm2phen> {
			?enhID obo:RO_0002331 ?disease
		}
	}

	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 ?tgene .
	}

        GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?tgene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?tgene_label ;
                        sio:SIO_010078 ?prot .
        }

        GRAPH <http://rdf.biogateway.eu/graph/prot> {
                ?prot skos:prefLabel ?prot_label .
        }
}

#2. Are these proteins involved in any biological process associated with the disease linked to the enhancer?
#inmmune response: GO_0006955; angiogenesis: GO_0001525; apoptosis process: GO_0006915; regulation of apoptosis process: GO_0042981; cell population proliferation: GO_0008283; ER-nucleus signaling pathway: GO_0006984

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX nuccore: <https://www.ncbi.nlm.nih.gov/nuccore/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT DISTINCT ?tgene_label ?prot_label ?bp ?bp_label
WHERE {
        GRAPH <http://rdf.biogateway.eu/graph/crm> {
                ?enhID rdfs:subClassOf obo:SO_0000727 ;
      			obo:GENO_0000894 ?start ;
                        obo:GENO_0000895 ?end ;
                        obo:BFO_0000050 nuccore:NC_000016.10 .
    					
                FILTER (?start <= 52565276 && ?end >= 52565276)
  	}

	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 ?tgene .
	}

        GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?tgene rdfs:subClassOf sio:SIO_010035 ;
                	skos:prefLabel ?tgene_label ;
                        sio:SIO_010078 ?prot .
        }

        GRAPH <http://rdf.biogateway.eu/graph/prot> {
                ?prot skos:prefLabel ?prot_label .
        }

	GRAPH <http://rdf.biogateway.eu/graph/prot2bp> {
                VALUES ?bp {
                        obo:GO_0006955
                        obo:GO_0001525
                        obo:GO_0006915
                        obo:GO_0042981
			obo:GO_0008283
			obo:GO_0006984
                }
		?prot obo:RO_0002331 ?bp
	}

	GRAPH <http://rdf.biogateway.eu/graph/go> {
        	?bp rdfs:label ?bp_label .
	}
}

#3. Multiple enhancers can collaborate in the regulation of one gene. What other enhancers can regulate the target gene in the breast tissue? We follow three different criteria:
#3.1. Enhancers provided by databases:
#(the gene is in the strand "-", so the TSS of the gene is the end position)

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?enh_label ?enh_start ?enh_end ?enh_middle ?TSS_gene ?distance
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 hgene:TOX3 .
	}

        GRAPH <http://rdf.biogateway.eu/graph/gene> {
                hgene:TOX3 obo:GENO_0000895 ?TSS_gene ;
			obo:BFO_0000050 ?chr .
        }

        GRAPH <http://rdf.biogateway.eu/graph/crm> {
                ?enhID rdfs:subClassOf obo:SO_0000727 ;
                        skos:prefLabel ?enh_label ;
			obo:GENO_0000894 ?enh_start ;
			obo:GENO_0000895 ?enh_end ;
			obo:BFO_0000050 ?chr .

		?enh_inst rdf:type ?enhID ;
			obo:TXPO_0003500 obo:UBERON_0000310 .
        }
        
        BIND((?enh_end - ?enh_start) AS ?enh_range)
        BIND((ROUND(?enh_range/2) + ?enh_start) AS ?enh_middle)
        BIND(ABS(?TSS_gene - ?enh_middle) AS ?distance)
}
ORDER BY ?distance


#3.2. Distance of 50 kb between the TSS of the gene and the center of the enhancer, because studies have reported that a large part of the typical enhancers are linked to genes within this distance 

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?enh_label ?enh_start ?enh_end ?enh_middle ?TSS_gene ?distance
WHERE {
        GRAPH <http://rdf.biogateway.eu/graph/gene> {
		hgene:TOX3 obo:GENO_0000895 ?TSS_gene ;
			obo:BFO_0000050 ?chr .
        }

        GRAPH <http://rdf.biogateway.eu/graph/crm> {
		?enh_ID rdfs:subClassOf obo:SO_0000727 ;
			skos:prefLabel ?enh_label ;
			obo:BFO_0000050 ?chr ;
			obo:GENO_0000894 ?enh_start ;
			obo:GENO_0000895 ?enh_end .

		?enh_inst rdf:type ?enh_ID ;
			obo:TXPO_0003500 obo:UBERON_0000310 .
        }
        BIND((?enh_end - ?enh_start) AS ?enh_range)
        BIND((ROUND(?enh_range/2) + ?enh_start) AS ?enh_middle)
        BIND(ABS(?TSS_gene - ?enh_middle) AS ?distance)
        FILTER(?distance <= 50000)
}
ORDER BY ?distance


#3.3. Enhancers located in the same TAD as the gene, because these domains act as regulatory modules

PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?enh_label ?enh_start ?enh_end ?enh_middle ?TSS_gene ?distance
WHERE {
        GRAPH <http://rdf.biogateway.eu/graph/gene> {
		hgene:TOX3 obo:GENO_0000895 ?TSS_gene ;
			obo:BFO_0000050 ?chr .
        }

        GRAPH <http://rdf.biogateway.eu/graph/tad> {
		?tad_ID obo:BFO_0000050 ?chr ;
			obo:GENO_0000894 ?tad_start ;
			obo:GENO_0000895 ?tad_end .

		?tad_inst rdf:type ?tad_ID ;
			obo:TXPO_0003500 obo:UBERON_0000310 .
        }
        FILTER(?tad_start < ?TSS_gene && ?tad_end > ?TSS_gene)

        GRAPH <http://rdf.biogateway.eu/graph/crm> {
		?enh_ID obo:BFO_0000050 ?chr ;
                        obo:GENO_0000894 ?enh_start ;
                        obo:GENO_0000895 ?enh_end ;
                	skos:prefLabel ?enh_label .

		?enh_inst rdf:type ?enh_ID ;
			obo:TXPO_0003500 obo:UBERON_0000310 .
        }
        FILTER (?enh_start > ?tad_start && ?enh_end < ?tad_end)

        BIND((?enh_end - ?enh_start) AS ?enh_range)
        BIND((ROUND(?enh_range/2) + ?enh_start) AS ?enh_middle)
        BIND(ABS(?TSS_gene - ?enh_middle) AS ?distance)
}
ORDER BY ?distance

#Quick version of the same query:

SELECT DISTINCT ?CRM ?CRM_start ?CRM_end ?TSS_gene ?Distance
WHERE {
    <http://rdf.biogateway.eu/gene/9606/TOX3> <http://purl.obolibrary.org/obo/BFO_0000050> ?chr .
    <http://rdf.biogateway.eu/gene/9606/TOX3> <http://purl.obolibrary.org/obo/GENO_0000895> ?TSS_gene .

    ?TAD <http://www.w3.org/2000/01/rdf-schema#subClassOf> <http://purl.obolibrary.org/obo/SO_0002304> .
    ?TAD_inst <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> ?TAD .
    ?TAD <http://purl.obolibrary.org/obo/BFO_0000050> ?chr .
    ?TAD_inst <http://purl.obolibrary.org/obo/TXPO_0003500> <http://purl.obolibrary.org/obo/UBERON_0000310> .
    ?TAD <http://purl.obolibrary.org/obo/GENO_0000895> ?TAD_end .
    ?TAD <http://purl.obolibrary.org/obo/GENO_0000894> ?TAD_start .
    ?chr <http://www.w3.org/2000/01/rdf-schema#subClassOf> <http://purl.obolibrary.org/obo/SO_0000340> .

    ?CRM <http://www.w3.org/2000/01/rdf-schema#subClassOf> <http://purl.obolibrary.org/obo/SO_0000727> .
    ?CRM_inst <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> ?CRM .
    ?CRM <http://purl.obolibrary.org/obo/BFO_0000050> ?chr .
    ?CRM_inst <http://purl.obolibrary.org/obo/TXPO_0003500> <http://purl.obolibrary.org/obo/UBERON_0000310> .
    ?CRM <http://purl.obolibrary.org/obo/GENO_0000895> ?CRM_end .
    ?CRM <http://purl.obolibrary.org/obo/GENO_0000894> ?CRM_start .

    BIND (?CRM_end - ?CRM_start AS ?Enh_range)
    BIND (?Enh_range / 2 AS ?Enh_div)
    BIND (?Enh_div + ?CRM_start AS ?Enh_middle)
    BIND (ABS(?TSS_gene - ?Enh_middle) AS ?Distance)

    FILTER ( ?TAD_start < ?TSS_gene ) .
    FILTER ( ?TAD_end > ?TSS_gene ) .
    FILTER ( ?CRM_start > ?TAD_start ) .
    FILTER ( ?CRM_end < ?TAD_end ) .
}
ORDER BY ?Distance


#4. If the enhancer is affected, in addition to the target gene provided by the databases, what other genes could be targeted by the same enhancer?

#4.1. Distance of 50 kb

PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX nuccore: <https://www.ncbi.nlm.nih.gov/nuccore/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
SELECT DISTINCT ?name_database ?enh_label ?start ?end ?enh_middle ?gene_label ?TSS_gene ?distance
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 hgene:TOX3 .
	}

        GRAPH <http://rdf.biogateway.eu/graph/crm> {
                ?enhID rdfs:subClassOf obo:SO_0000727 ;
      			obo:GENO_0000894 ?start;
                        obo:GENO_0000895 ?end ;
                        obo:BFO_0000050 nuccore:NC_000016.10 ;
                        skos:prefLabel ?enh_label .

		?enh_inst rdf:type ?enhID ;
                        sio:SIO_000253 ?database .

    		?database skos:prefLabel ?name_database .
                FILTER (?start <= 52565276 && ?end >= 52565276)
	}

	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		{
		?gene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?gene_label ;
			obo:BFO_0000050 nuccore:NC_000016.10 ;
			obo:GENO_0000906 faldo:ForwardStrandPosition ;
			obo:GENO_0000894 ?TSS_gene .
		} UNION {
		?gene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?gene_label ;
			obo:BFO_0000050 nuccore:NC_000016.10 ;
			obo:GENO_0000906 faldo:ReverseStrandPosition ;
			obo:GENO_0000895 ?TSS_gene .
		}
  	}

	BIND((?end - ?start) AS ?enh_range)
        BIND((ROUND(?enh_range/2) + ?start) AS ?enh_middle)
        BIND(ABS(?TSS_gene - ?enh_middle) AS ?distance)
	FILTER(?distance <= 50000)
}
ORDER BY ?distance


# 4.2. Distance of 1 MB

PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX nuccore: <https://www.ncbi.nlm.nih.gov/nuccore/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
SELECT DISTINCT ?name_database ?enh_label ?start ?end ?enh_middle ?gene_label ?TSS_gene ?distance
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 hgene:TOX3 .
	}

        GRAPH <http://rdf.biogateway.eu/graph/crm> {
                ?enhID rdfs:subClassOf obo:SO_0000727 ;
      			obo:GENO_0000894 ?start;
                        obo:GENO_0000895 ?end ;
                        obo:BFO_0000050 nuccore:NC_000016.10 ;
                        skos:prefLabel ?enh_label .

		?enh_inst rdf:type ?enhID ;
                        sio:SIO_000253 ?database .

    		?database skos:prefLabel ?name_database .
                FILTER (?start <= 52565276 && ?end >= 52565276)
	}

	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		{
		?gene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?gene_label ;
			obo:BFO_0000050 nuccore:NC_000016.10 ;
			obo:GENO_0000906 faldo:ForwardStrandPosition ;
			obo:GENO_0000894 ?TSS_gene .
		} UNION {
		?gene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?gene_label ;
			obo:BFO_0000050 nuccore:NC_000016.10 ;
			obo:GENO_0000906 faldo:ReverseStrandPosition ;
			obo:GENO_0000895 ?TSS_gene .
		}
  	}

	BIND((?end - ?start) AS ?enh_range)
        BIND((ROUND(?enh_range/2) + ?start) AS ?enh_middle)
        BIND(ABS(?TSS_gene - ?enh_middle) AS ?distance)
	FILTER(?distance <= 1000000)
}
ORDER BY ?distance

#Are any of these genes involved in any biological process associated with the disease?
PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX nuccore: <https://www.ncbi.nlm.nih.gov/nuccore/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
SELECT DISTINCT ?name_database ?enh_label ?start ?end ?enh_middle ?gene_label ?TSS_gene ?distance ?prot_label ?bp_label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 hgene:TOX3 .
	}

        GRAPH <http://rdf.biogateway.eu/graph/crm> {
                ?enhID rdfs:subClassOf obo:SO_0000727 ;
      			obo:GENO_0000894 ?start;
                        obo:GENO_0000895 ?end ;
                        obo:BFO_0000050 nuccore:NC_000016.10 ;
                        skos:prefLabel ?enh_label .

		?enh_inst rdf:type ?enhID ;
                        sio:SIO_000253 ?database .

    		?database skos:prefLabel ?name_database .
                FILTER (?start <= 52565276 && ?end >= 52565276)
	}

	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		{
		?gene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?gene_label ;
			obo:BFO_0000050 nuccore:NC_000016.10 ;
			obo:GENO_0000906 faldo:ForwardStrandPosition ;
			obo:GENO_0000894 ?TSS_gene .
		} UNION {
		?gene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?gene_label ;
			obo:BFO_0000050 nuccore:NC_000016.10 ;
			obo:GENO_0000906 faldo:ReverseStrandPosition ;
			obo:GENO_0000895 ?TSS_gene .
		}
		?gene sio:SIO_010078 ?prot
  	}

	BIND((?end - ?start) AS ?enh_range)
        BIND((ROUND(?enh_range/2) + ?start) AS ?enh_middle)
        BIND(ABS(?TSS_gene - ?enh_middle) AS ?distance)
	FILTER(?distance <= 1000000)

	GRAPH <http://rdf.biogateway.eu/graph/prot> {
             	?prot skos:prefLabel ?prot_label .
       	}

	GRAPH <http://rdf.biogateway.eu/graph/prot2bp> {
		VALUES ?bp {
                        obo:GO_0006955
                        obo:GO_0001525
                        obo:GO_0006915
                        obo:GO_0042981
			obo:GO_0008283
			obo:GO_0006984
                }
		?prot obo:RO_0002331 ?bp .
	}
	
	GRAPH <http://rdf.biogateway.eu/graph/go> {
		?bp rdfs:label ?bp_label .
	}
}
ORDER BY ?distance


#5. How many transcription factors are associated with the enhancers or the target genes, how many other genes could be target of these TFs and how many other proteins interact with these TFs?

#5.1. Enhancer-dbTF

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX nuccore: <https://www.ncbi.nlm.nih.gov/nuccore/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT *
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		?enhID rdfs:subClassOf obo:SO_0000727 ;
      			obo:GENO_0000894 ?start;
                        obo:GENO_0000895 ?end ;
                        obo:BFO_0000050 nuccore:NC_000016.10 .
    					
                FILTER (?start <= 52565276 && ?end >= 52565276)	
	}

	GRAPH <http://rdf.biogateway.eu/graph/crm2gene> {
		?enhID obo:RO_0002429 ?tgene .
	}

	GRAPH <http://rdf.biogateway.eu/graph/crm2tfac> {
		?enhID obo:RO_0002436 ?tf .
	}

	GRAPH <http://rdf.biogateway.eu/graph/prot> {
		?tf skos:prefLabel ?tf_label .
	}
}


#5.2. dbTF-TOX3 Gene

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT DISTINCT ?tf ?tf_label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/tfac2gene> {
		?int rdf:object hgene:TOX3 ;
			rdf:subject ?tf .
	}
        GRAPH <http://rdf.biogateway.eu/graph/prot> {
		?tf skos:prefLabel ?tf_label
	}
}


#5.3. dbTF-target genes and protein interactions

PREFIX hgene: <http://rdf.biogateway.eu/gene/9606/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
SELECT COUNT(DISTINCT ?tf) COUNT(DISTINCT ?gene) COUNT(DISTINCT ?prot)
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/tfac2gene> {
		?tf obo:RO_0002428 hgene:TOX3 ;
			obo:RO_0002428 ?gene .
	}

	OPTIONAL {
	GRAPH <http://rdf.biogateway.eu/graph/prot2prot> {
		?tf obo:RO_0002436 ?prot .
		}
	}
}