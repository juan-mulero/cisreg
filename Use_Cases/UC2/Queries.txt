#We are interested in the study of the "breast cancer" phenotype, so we use the network to obtain information about the regulation around this phenotype.
#1. Using OMIM as reference, which phenotypes are subject to study because they contain "breast cancer" in their preferred or alternative label?

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT DISTINCT  ?phen ?label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/omim> {
		{?phen skos:prefLabel ?label}
		UNION
		{?phen skos:altLabel ?label}
	}
        FILTER regex(?label, "breast cancer", "i")
}


#2. What genes are involved with these phenotypes? What proteins are encoded by these genes and in what biological processes are these proteins involved?

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?phen ?gene_label ?prot_label ?bp_label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/omim> {
		{?phen skos:prefLabel ?label}
		UNION
		{?phen skos:altLabel ?label}
	}
        FILTER regex(?label, "breast cancer", "i")

	GRAPH <http://rdf.biogateway.eu/graph/gene2phen> {
		?gene2phe rdf:object ?phen ;
			rdf:subject ?gene .
	}	

	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?gene skos:prefLabel ?gene_label ;
			sio:SIO_010078 ?prot
        }

	GRAPH <http://rdf.biogateway.eu/graph/prot> {
               	?prot skos:prefLabel ?prot_label .
        }

	OPTIONAL {
		GRAPH <http://rdf.biogateway.eu/graph/prot2bp> {
			?prot2bp rdf:subject ?prot ;
				rdf:object ?bp .
		}

		GRAPH <http://rdf.biogateway.eu/graph/go> {
			?bp rdfs:label ?bp_label .
		}
	}
}
ORDER BY ?phen ?gene_label ?prot_label ?bp_label
 

#3.1. What enhancers are involved in the regulation of these genes? 
#Since we are discussing the "breast cancer" phenotype, have any of these enhancers been identified in any cell line related to breast tissue? 

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT DISTINCT ?phen ?gene_label ?enh_label ?database_label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/omim> {
		{?phen skos:prefLabel ?label}
		UNION
		{?phen skos:altLabel ?label}
	}
        FILTER regex(?label, "breast cancer", "i")

	GRAPH <http://rdf.biogateway.eu/graph/gene2phen> {
		?gene2phe rdf:object ?phen ;
			rdf:subject ?gene .
	}

	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		?enhID rdf:type owl:Class ;
			skos:prefLabel ?enh_label ;
                        obo:RO_0002428 ?gene ;
			obo:CLO_0037208 obo:UBERON_0000310 ;
			obo:CLO_0037210 ?cell_line ;
			sio:SIO_000253 ?database .
		?database skos:prefLabel ?database_label .
		FILTER regex(?cell_line, "/CLO_")
	}
	
	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?gene skos:prefLabel ?gene_label
	}
}


#Count of cell lines:

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT COUNT(DISTINCT ?cell_line)
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/omim> {
		{?phen skos:prefLabel ?label}
		UNION
		{?phen skos:altLabel ?label}
	}
        FILTER regex(?label, "breast cancer", "i")

	GRAPH <http://rdf.biogateway.eu/graph/gene2phen> {
		?gene2phe rdf:object ?phen ;
			rdf:subject ?gene .
	}

	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		?enhID rdf:type owl:Class ;
			skos:prefLabel ?enh_label ;
                        obo:RO_0002428 ?gene ;
			obo:CLO_0037208 obo:UBERON_0000310 ;
			obo:CLO_0037210 ?cell_line ;
			sio:SIO_000253 ?database .
		?database skos:prefLabel ?database_label .
		FILTER regex(?cell_line, "/CLO_")
	}
	
	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?gene skos:prefLabel ?gene_label
	}
}


#3.2. In what biological processes are the genes regulated by these enhancers involved?

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT DISTINCT ?tgene_label ?prot_label ?bp_label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/omim> {
		{?phen skos:prefLabel ?label}
		UNION
		{?phen skos:altLabel ?label}
	}
        FILTER regex(?label, "breast cancer", "i")

	GRAPH <http://rdf.biogateway.eu/graph/gene2phen> {
		?gene2phe rdf:object ?phen ;
			rdf:subject ?gene .
	}

	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		?enhID rdf:type owl:Class ;
			skos:prefLabel ?enh_label ;
                        obo:RO_0002428 ?gene ;
			obo:RO_0002428 ?tgene ;
			obo:CLO_0037208 obo:UBERON_0000310 ;
			obo:CLO_0037210 ?cell_line ;
			sio:SIO_000253 ?database .
		?database skos:prefLabel ?database_label .
	}
	
	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?tgene skos:prefLabel ?tgene_label ;
			sio:SIO_010078 ?prot .
        }

	GRAPH <http://rdf.biogateway.eu/graph/prot> {
               	?prot skos:prefLabel ?prot_label .
        }

	GRAPH <http://rdf.biogateway.eu/graph/prot2bp> {
		?prot2bp rdf:subject ?prot ;
			rdf:object ?bp .
	}

	GRAPH <http://rdf.biogateway.eu/graph/go> {
		?bp rdfs:label ?bp_label .
	}
}
ORDER BY ?tgene_label ?prot_label ?bp_label


#4. What enhancers are linked in the network to breast cancer? What are the target genes and the TFs linked to enhancers?

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT DISTINCT ?enh_label ?tgene ?tf
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		VALUES ?phen {
			<http://purl.bioontology.org/ontology/OMIM/114480>
			<http://purl.obolibrary.org/obo/DOID_1612>
			<https://id.nlm.nih.gov/mesh/D001943>
		}
		?enhID rdf:type owl:Class ;
			skos:prefLabel ?enh_label ;
			obo:RO_0002331 ?phen ;
			sio:SIO_000253 ?database .
		?database skos:prefLabel ?database_label .
	}
	OPTIONAL {
		?enhID obo:RO_0002428 ?tgene .
		?tgene rdfs:subClassOf sio:SIO_010035
	}
	OPTIONAL {
		?enhID obo:RO_0002436 ?tf .
		?tf rdfs:subClassOf obo:NCIT_C17207 .
	}
}
ORDER BY ?enh_label

#Count:
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT COUNT(DISTINCT ?enhID) COUNT(DISTINCT ?tgene) COUNT(DISTINCT ?tf)
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		VALUES ?phen {
			<http://purl.bioontology.org/ontology/OMIM/114480>
			<http://purl.obolibrary.org/obo/DOID_1612>
			<https://id.nlm.nih.gov/mesh/D001943>
		}
		?enhID rdf:type owl:Class ;
			skos:prefLabel ?enh_label ;
			obo:RO_0002331 ?phen ;
			sio:SIO_000253 ?database .
		?database skos:prefLabel ?database_label .
	}
	OPTIONAL {
		?enhID obo:RO_0002428 ?tgene .
		?tgene rdfs:subClassOf sio:SIO_010035 .
	}
	OPTIONAL {
		?enhID obo:RO_0002436 ?tf .
		?tf rdfs:subClassOf obo:NCIT_C17207 .
	}
}


#5. What dbTFs cooperate in the transcription process of these target genes?

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT DISTINCT ?tgene_label ?tf_label
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		VALUES ?phen {
			<http://purl.bioontology.org/ontology/OMIM/114480>
			<http://purl.obolibrary.org/obo/DOID_1612>
			<https://id.nlm.nih.gov/mesh/D001943>
		}
		?enhID rdf:type owl:Class ;
			obo:RO_0002331 ?phen ;
			obo:RO_0002428 ?tgene .
	}
	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?tgene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?tgene_label .
	}
	GRAPH <http://rdf.biogateway.eu/graph/tfac2gene> {
		?int rdf:object ?tgene ;
			rdf:subject ?tf .
	}
        GRAPH <http://rdf.biogateway.eu/graph/prot> {
		?tf skos:prefLabel ?tf_label .
	}
}

#Count:
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT COUNT(DISTINCT ?tgene_label) COUNT(DISTINCT ?tf_label)
WHERE {
	GRAPH <http://rdf.biogateway.eu/graph/crm> {
		VALUES ?phen {
			<http://purl.bioontology.org/ontology/OMIM/114480>
			<http://purl.obolibrary.org/obo/DOID_1612>
			<https://id.nlm.nih.gov/mesh/D001943>
		}
		?enhID rdf:type owl:Class ;
			obo:RO_0002331 ?phen ;
			obo:RO_0002428 ?tgene .
	}
	GRAPH <http://rdf.biogateway.eu/graph/gene> {
		?tgene rdfs:subClassOf sio:SIO_010035 ;
			skos:prefLabel ?tgene_label .
	}
	GRAPH <http://rdf.biogateway.eu/graph/tfac2gene> {
		?int rdf:object ?tgene ;
			rdf:subject ?tf .
	}
        GRAPH <http://rdf.biogateway.eu/graph/prot> {
		?tf skos:prefLabel ?tf_label .
	}
}
